#
# Platinenmacher Linux for embedded devices based on poky
#
require conf/distro/poky.conf

DISTRO_NAME = "Platinenmacher Linux (based on poky)"
POKY_VERSION := "${DISTRO_VERSION}"
DISTRO_VERSION = "2026.01.2-poky-${POKY_VERSION}"
DISTRO = "platinenmacher-linux"
DISTROOVERRIDES = "poky:platinenmacher-linux"

VENDOR = "platinenmacher"
TARGET_VENDOR = "-${VENDOR}"

LICENSE_FLAGS_ACCEPTED = "commercial"

# We use systemd as init manager
POKY_INIT_MANAGER:platinenmacher-linux = "systemd"

# Add extra DISTRO_FEATUREs
DISTRO_FEATURES:append = " pam usrmerge"

# Use our alternate kernel version
PREFERRED_VERSION_linux-yocto = "6.18%"

# Create /etc/build
INHERIT += "image-buildinfo"
IMAGE_BUILDINFO_VARS:append = " DATETIME DISTRO_NAME IMAGE_BASENAME MACHINE TUNE_PKGARCH"
IMAGE_BUILDINFO_VARS:append = " MACHINE_FEATURES DISTRO_FEATURES IMAGE_FEATURES"

# All images build with the distribution get the version package
IMAGE_INSTALL:append = " os-release"

# QA settings
INHERIT += "sanity"
INHERIT += "insane"
# configure insane.bbclass
ERROR_TO_WARN_QA = "version-going-backwards"
ERROR_QA:remove = "${ERROR_TO_WARN_QA}"
WARN_QA:append = " ${ERROR_TO_WARN_QA}"

VIRTUAL-RUNTIME_syslog = ""
VIRTUAL-RUNTIME_initscripts = ""

# default value in bitbake.conf of poky is '/home/root'
# change to '/root' as this is preferred path
ROOT_HOME ?= "/root"

# Use iwd instead of wpa-supplicant for WiFi management
WIRELESS_DAEMON = "iwd"

# Completely blacklist wpa-supplicant to prevent it from being included
PACKAGE_EXCLUDE += "wpa-supplicant"

DISTRO_FEATURES_DEFAULT:remove = "pcmcia zeroconf 3g"
DISTRO_FEATURES = "${DISTRO_FEATURES_DEFAULT} \
                   bluetooth \
                   crun \
                   kernelmodsign \
                   opengl \
                   pam \
                   security \
                   swupdate \
    ${@bb.utils.filter('MACHINE_FEATURES', 'tpm tpm2', d)} \
                   usbgadget \
                   virtualization \
                   wayland \
                   x-wayland \
                   wifi \
"
